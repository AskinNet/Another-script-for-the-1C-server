// askin(c)
//
// Параметры скрипта:
// Первый параметр - наименования файла настроек
// Второй - команда.
//
// Команды:
// без команды  - ничего
// testparam
// или debug				- только тестирование параметров
// lock 					- только блокировка
// unlock 					- только разблокировка
// restart      			- только перезагрузка сервера
// updateFromStorage 		- только обновление из хранилищ
// updateDB 				- обновление конфигурации базы
// batch       				- пакетный режим, команды и их очередность прописывается в файле настроек




// BSLLS-off
#Область ОписаниеПеременных

#Использовать v8runner
#Использовать configor
#Использовать irac
#Использовать 1commands
#Использовать asserts
#Использовать fs
#Использовать cmdline

Перем Лог;
Перем МенеджерПараметров;
Перем МассивОбслуживаемыхБаз;
Перем ИмяСервераRAS;
Перем ПроцессRAS;

#КонецОбласти





#Область ПрограммныйИнтерфейс

Функция ИнициализацияОкружения()
	
	// значение по умолчанию
	ФайлНастроек = "tests.yml";
	КомандаСкрипта = "";
	
	Лог = Логирование.ПолучитьЛог("oscript.app.serv");
	Лог.УстановитьРаскладку(ЭтотОбъект);
	
	ЛогВыводВФайл = Новый ВыводЛогаВФайл; //аппендер обычного лога
	ЛогВыводВФайл.ОткрытьФайл("svc1c.log");
	Лог.ДобавитьСпособВывода(ЛогВыводВФайл, УровниЛога.Информация);
	Лог.Информация("Инициализация скрипта");
	
	Если АргументыКоманднойСтроки.Количество() > 0 Тогда
		ФайлНастроек = ОбъединитьПути(ТекущийКаталог(), АргументыКоманднойСтроки[0]);
		Если АргументыКоманднойСтроки.Количество() > 1 Тогда
			КомандаСкрипта = АргументыКоманднойСтроки[1];
		КонецЕсли;
	КонецЕсли;
	
	Если ФайлНастроек = "" Тогда
		Лог.Ошибка("Не найден файл параметров");
		ЗавершитьРаботуСкрипта(1);
	КонецЕсли;
	
	МенеджерПараметров = Новый МенеджерПараметров();
	МенеджерПараметров.АвтоНастройка(ФайлНастроек);
	МенеджерПараметров.ИспользоватьПровайдерYAML(0);
	МенеджерПараметров.УстановитьФайлПараметров(ФайлНастроек);
	МенеджерПараметров.Прочитать();
	Если НЕ МенеджерПараметров.ЧтениеВыполнено() Тогда
		Лог.Информация("Количество аргументов: " + АргументыКоманднойСтроки.Количество());
		Лог.Информация("Файл настроек: " + ФайлНастроек);
		Лог.Информация("Команда скрипта: " + КомандаСкрипта);
		Лог.Ошибка("Не удалось прочитать настройки!");
		ЗавершитьРаботуСкрипта(1);
	КонецЕсли;
	
	Если МенеджерПараметров.Параметр("debug") Тогда
		ФайлДляЛогаОтладки = Новый ВыводЛогаВФайл;
		ФайлДляЛогаОтладки.ОткрытьФайл("svc1c_debug.log");
		Лог.ДобавитьСпособВывода(ФайлДляЛогаОтладки, УровниЛога.Отладка);
		Лог.УстановитьУровень(УровниЛога.Отладка);
	КонецЕсли;

	ИмяСервераRAS = МенеджерПараметров.Параметр("serverRAS.servernameRAS") + ":"
		+ МенеджерПараметров.Параметр("serverRAS.portRAS");
	
	Если НуженЗапускRAS() Тогда
		ЗапуститьRAS();
	КонецЕсли;
	
	МассивОбслуживаемыхБаз = МассивОбслуживаемыхБаз();
	
	Возврат КомандаСкрипта;
	
КонецФункции

Функция ПолучениеСоединенийСКластером(ТестированиеПодключения = Ложь)
	
	Попытка
		АдминистраторКластера1С = МенеджерПараметров.Параметр("server1c.clusterAdmin");
		ПарольКластера1С = МенеджерПараметров.Параметр("server1c.clusterAdminPassword");
		Админ = Новый Структура("Администратор, Пароль", АдминистраторКластера1С, ПарольКластера1С);

		АгентКластера = Новый УправлениеКластером1С("8.3", ИмяСервераRAS, Админ);
		АгентКластера.УстановитьИсполнительКоманд(Новый ИсполнительКоманд("8.3"));

		СписокКластеров = Новый Кластеры(АгентКластера);
		СписокКластеров.ОбновитьДанные(1);

		//Кластер = АгентКластера.Кластеры().Список()[0];
		Кластер = СписокКластеров.Список()[0];
		
		Результат = Истина;
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
		Если НЕ ТестированиеПодключения Тогда 
			Лог.Ошибка("Ошибка подключения к кластеру: (" + ОписаниеОшибки + ")");
		КонецЕсли;
		Результат = Ложь;
		Кластер = Неопределено;
	КонецПопытки;
	
	Возврат Новый Структура("Результат,Кластер", Результат, Кластер);
	
КонецФункции

Процедура УдалениеСеансовСКонфигуратором(Знач СписокБаз)

	РезультатПодключения = ПолучениеСоединенийСКластером();
	Если РезультатПодключения.Результат Тогда
		ТекущийКластер = РезультатПодключения.Кластер;
	Иначе
		//ЗавершитьРаботуСкрипта(1);
		Возврат;
	КонецЕсли;

	Для Каждого ИмяТекущейБазы Из СписокБаз Цикл

		ИнформационнаяБаза = ПараметрыСоединенияСИнформационнойБазой(ИмяТекущейБазы,ТекущийКластер);
		ИдТекущейБазы = ИнформационнаяБаза.Получить("infobase");
		Сеансы = ТекущийКластер.Сеансы();
		Для Каждого Сеанс Из Сеансы.Список() Цикл
			Параметры = Сеанс.ПараметрыОбъекта();
			Приложение = Сеанс.Получить("Приложение");
			БазаДанныхСеанса = Сеанс.Получить("infobase");
			Если Приложение = "Designer" И БазаДанныхСеанса = ИдТекущейБазы Тогда
				Сеанс.Завершить();
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалениеВсехСеансов(Знач СписокБаз)
	
	РезультатПодключения = ПолучениеСоединенийСКластером();
	Если РезультатПодключения.Результат Тогда
		ТекущийКластер = РезультатПодключения.Кластер;
	Иначе
		//ЗавершитьРаботуСкрипта(1);
		Возврат;
	КонецЕсли;

	Для Каждого ИмяТекущейБазы Из СписокБаз Цикл

		Сеансы = ТекущийКластер.Сеансы();
		Для Каждого Сеанс Из Сеансы.Список() Цикл
			Сеанс.Завершить();
		КонецЦикла;
		Лог.Информация("Разорваны сеансы с базой " + ИмяТекущейБазы);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура БлокировкаНаБазы(Знач СписокБаз, Установить = Ложь)
	
	Если Установить Тогда
		ПереключательСостояния = Перечисления.СостоянияВыключателя.Включено;
	Иначе
		ПереключательСостояния = Перечисления.СостоянияВыключателя.Выключено;
	КонецЕсли;
	
	РезультатПодключения = ПолучениеСоединенийСКластером();
	Если РезультатПодключения.Результат Тогда
		ТекущийКластер = РезультатПодключения.Кластер;
	Иначе
		//ЗавершитьРаботуСкрипта(1);
		Возврат;
	КонецЕсли;

	Для Каждого ИмяТекущейБазы Из СписокБаз Цикл
		
		ИнформационнаяБаза = ПараметрыСоединенияСИнформационнойБазой(ИмяТекущейБазы,ТекущийКластер);	
		ПараметрыИБ = Новый Структура();		
		НачалоБлокировкиСеансов = ТекущаяДата();
		ОкончаниеБлокировкиСеансов = НачалоБлокировкиСеансов + Число(МенеджерПараметров.Параметр("server1c.lockTime"));	
		ПараметрыИБ.Вставить("НачалоБлокировкиСеансов", НачалоБлокировкиСеансов);
		ПараметрыИБ.Вставить("ОкончаниеБлокировкиСеансов", ОкончаниеБлокировкиСеансов);
		ПараметрыИБ.Вставить("СообщениеБлокировкиСеансов", "Технические работы");
		ПараметрыИБ.Вставить("КодРазрешения", МенеджерПараметров.Параметр("server1c.bases." + ИмяТекущейБазы + ".allowKey"));
		ПараметрыИБ.Вставить("БлокировкаСеансовВключена", ПереключательСостояния);
		ПараметрыИБ.Вставить("БлокировкаРегламентныхЗаданийВключена", ПереключательСостояния);

		Попытка
			ИнформационнаяБаза.Изменить(ПараметрыИБ);
		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
			Лог.Ошибка("Ошибка установки блокировки: (" + ОписаниеОшибки + ")");
		КонецПопытки;
		
		Если Установить Тогда
			Лог.Информация("Заблокирована база:" + ИмяТекущейБазы);
		Иначе
			Лог.Информация("Разблокированна база:" + ИмяТекущейБазы);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ОбновлениеКонфигурацииИзХранилища(База, Конфигуратор = Неопределено)
	
	Если Конфигуратор = Неопределено Тогда
		Конфигуратор = ПараметрыКонфигуратора(База);
	КонецЕсли;
	СтрокаХранилищ = "server1c.bases." + База + ".1cstorages";
	Для Каждого Хранилище Из МенеджерПараметров.Параметр(СтрокаХранилищ) Цикл
		ИмяОбъектаХранилища = Хранилище.Ключ;
		ИмяХранилища = МенеджерПараметров.Параметр(СтрокаХранилищ + "." + ИмяОбъектаХранилища + ".name");
		Попытка
			Если ИмяХранилища = "Main" Тогда
				Лог.Информация("Запуск обновления основной конфигурации из хранилища");
				
				Лог.Отладка("Путь к хранилищу: " + МенеджерПараметров.Параметр(СтрокаХранилищ + "." + ИмяОбъектаХранилища + ".path"));
				Лог.Отладка("Пользователь хранилища: " + МенеджерПараметров.Параметр(СтрокаХранилищ + "." + ИмяОбъектаХранилища + ".user"));
				Лог.Отладка("Пароль хранилища: " + МенеджерПараметров.Параметр(СтрокаХранилищ + "." + ИмяОбъектаХранилища + ".pass"));
				
				Конфигуратор.ЗагрузитьКонфигурациюИзХранилища(МенеджерПараметров.Параметр(СтрокаХранилищ + "." + ИмяОбъектаХранилища + ".path"),
					МенеджерПараметров.Параметр(СтрокаХранилищ + "." + ИмяОбъектаХранилища + ".user"),
					МенеджерПараметров.Параметр(СтрокаХранилищ + "." + ИмяОбъектаХранилища + ".pass"));
				
				Лог.Информация("Основная конфигурация обновлена из хранилища");
			Иначе
				Лог.Информация("Запуск обновления расширения " + ИмяХранилища + " из хранилища");
				
				Лог.Отладка("Путь к хранилищу расширения: " + МенеджерПараметров.Параметр(СтрокаХранилищ + "." + ИмяОбъектаХранилища + ".path"));
				Лог.Отладка("Пользователь хранилища расширения: " + МенеджерПараметров.Параметр(СтрокаХранилищ + "." + ИмяОбъектаХранилища + ".user"));
				Лог.Отладка("Пароль хранилища расширения: " + МенеджерПараметров.Параметр(СтрокаХранилищ + "." + ИмяОбъектаХранилища + ".pass"));
				
				Конфигуратор.РасширениеПолучитьИзХранилища(МенеджерПараметров.Параметр(СтрокаХранилищ + "." + ИмяОбъектаХранилища + ".path"),
					МенеджерПараметров.Параметр(СтрокаХранилищ + "." + ИмяОбъектаХранилища + ".user"),
					МенеджерПараметров.Параметр(СтрокаХранилищ + "." + ИмяОбъектаХранилища + ".pass"),
					ИмяХранилища, 0);
				Лог.Информация("Расширение " + ИмяХранилища + " обновлено из хранилища");
			КонецЕсли;
		Исключение
			Лог.Ошибка(СтрШаблон(НСтр("ru = 'Ошибка при получении изменений из хранилища расширения
						|%1'"), Конфигуратор.ВыводКоманды()));
		КонецПопытки;
	КонецЦикла;
	
	Конфигуратор = NULL;
	ФайлПлатформы = ОбъединитьПути(ТекущийКаталог(), "tt.txt");
	Если ФС.ФайлСуществует(ФайлПлатформы) Тогда
		ФС.УдалитьФайлы(ФайлПлатформы);
	КонецЕсли;
	ВыполнитьСборкуМусора();
	
КонецФункции

Функция ОбновлениеКонфигурации(База, Конфигуратор = Неопределено)
	
	Если Конфигуратор = Неопределено Тогда
		Конфигуратор = ПараметрыКонфигуратора(База);
	КонецЕсли;
	
	СтрокаХранилищ = "server1c.bases." + База + ".1cstorages";
	Для Каждого Хранилище Из МенеджерПараметров.Параметр(СтрокаХранилищ) Цикл
		ИмяОбъектаХранилища = Хранилище.Ключ;
		ИмяХранилища = МенеджерПараметров.Параметр(СтрокаХранилищ + "." + ИмяОбъектаХранилища + ".name");
		Попытка
			Если ИмяХранилища = "Main" Тогда
				Лог.Информация("Запуск обновления основной конфигурации базы");
				Конфигуратор.ОбновитьКонфигурациюБазыДанных();
				Лог.Информация("Обновление основной конфигурации базы завершено");
			Иначе
				Лог.Информация("Запуск обновления расширения базы: " + ИмяХранилища);
				Конфигуратор.ОбновитьКонфигурациюБазыДанных( , , , ИмяХранилища);
				Лог.Информация("Обновление расширения " + ИмяХранилища + " завершено");
			КонецЕсли;
		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
			Лог.Ошибка(СтрШаблон(НСтр("ru = 'Ошибка при обновлении
						|%1'"), Конфигуратор.ВыводКоманды()));
		КонецПопытки;
	КонецЦикла;
	
	Конфигуратор = NULL;
	ФайлПлатформы = ОбъединитьПути(ТекущийКаталог(), "tt.txt");
	Если ФС.ФайлСуществует(ФайлПлатформы) Тогда
		ФС.УдалитьФайлы(ФайлПлатформы);
	КонецЕсли;
	ВыполнитьСборкуМусора();
	
КонецФункции

Процедура ПерезапускСервера()
	
	КомандныйФайл = Новый КомандныйФайл;
	КомандныйФайл.Создать();
	
	Лог.Отладка("-----Скрипт bat-------");
	Лог.Информация("Перезапуск серверных служб 1С");
	
	КомандныйФайл.ДобавитьКоманду("@SC STOP """ + МенеджерПараметров.Параметр("server1c.1cDemonName") + "");
	//КомандныйФайл.ДобавитьКоманду("@SC STOP """ + МенеджерПараметров.Параметр("server1c.1cRasDemonName") + "");
	Лог.Отладка("Пауза 2 секунды");
	КомандныйФайл.ДобавитьКоманду("@start /wait timeout 2");
	Лог.Отладка("Убийство неуспевших завершиться процессов");
	КомандныйФайл.ДобавитьКоманду("@TASKKILL /F /IM RPHOST.EXE");
	КомандныйФайл.ДобавитьКоманду("@TASKKILL /F /IM RMNGR.EXE");
	КомандныйФайл.ДобавитьКоманду("@TASKKILL /F /IM RAGENT.EXE");
	//КомандныйФайл.ДобавитьКоманду("@TASKKILL /F /IM RAS.EXE");
	
	Лог.Информация("Очистка каталога сеансовых данных сервера 1с");
	КомандныйФайл.ДобавитьКоманду("@DEL /Q /F /S " + МенеджерПараметров.Параметр("server1c.Path1cService") + "\SNCCNTX*");
	КомандныйФайл.ДобавитьКоманду("@DEL /Q /F /S " + МенеджерПараметров.Параметр("server1c.Path1cService") + "\STT*");
	
	Лог.Отладка("Запуск служб");
	КомандныйФайл.ДобавитьКоманду("@SC START """ + МенеджерПараметров.Параметр("server1c.1cDemonName") + "");
	//КомандныйФайл.ДобавитьКоманду("@SC START """ + МенеджерПараметров.Параметр("server1c.1cRasDemonName") + "");
	
	КомандныйФайл.ДобавитьКоманду("@start /wait timeout 5");
	
	КодВозврата = КомандныйФайл.Исполнить();
	ЭхоБатФайла = КомандныйФайл.ПолучитьВывод();
	Лог.Отладка("Код возврата " + КодВозврата);
	
	Лог.Отладка("Лог bat-файла - начало");
	Лог.Отладка(ЭхоБатФайла);
	Лог.Отладка("Лог bat-файла - окончание");
	
	Лог.Информация("Перезапуск серверных служб завешен");
	
	Приостановить(1000 * 5);
	
	Лог.Информация("Тестовое подключение к кластеру после рестарта");
	ПолучениеСоединенийСКластером(Истина);
	Лог.Информация("Тестовое подключение к кластеру после рестарта завершено");
	
КонецПроцедуры

Функция ТестированиеПараметров()
	
	ПрочитанныеПараметры = МенеджерПараметров.ПрочитанныеПараметры();
	
	Попытка
		// проверки наличия ключей
		Лог.Отладка("Тестирование параметра " + "batchМode");
		Ожидаем.Что(МенеджерПараметров.Параметр("batchМode")).Существует();

		Лог.Отладка("Тестирование параметра " + "debug");
		Ожидаем.Что(МенеджерПараметров.Параметр("debug")).Существует();		

		Лог.Отладка("Тестирование параметра " + "server1c.server");
		Ожидаем.Что(МенеджерПараметров.Параметр("server1c.server")).Существует();
		Лог.Отладка("Тестирование параметра " + "server1c.port");
		Ожидаем.Что(МенеджерПараметров.Параметр("server1c.port")).Существует();
		Лог.Отладка("Тестирование параметра " + "serverRAS.servernameRAS");
		Ожидаем.Что(МенеджерПараметров.Параметр("serverRAS.servernameRAS")).Существует();
		Лог.Отладка("Тестирование параметра " + "serverRAS.portRAS");
		Ожидаем.Что(МенеджерПараметров.Параметр("serverRAS.portRAS")).Существует();
		Лог.Отладка("Тестирование параметра " + "server1c.version");
		Ожидаем.Что(МенеджерПараметров.Параметр("server1c.version")).Существует();
		Лог.Отладка("Тестирование параметра " + "server1c.1cDemonName");
		Ожидаем.Что(МенеджерПараметров.Параметр("server1c.1cDemonName")).Существует();
		Лог.Отладка("Тестирование параметра " + "serverRAS.1cRasDemonName");
		Ожидаем.Что(МенеджерПараметров.Параметр("serverRAS.1cRasDemonName")).Существует();
		Лог.Отладка("Тестирование параметра " + "server1c.Path1cService");
		Ожидаем.Что(МенеджерПараметров.Параметр("server1c.Path1cService")).Существует();
		Лог.Отладка("Тестирование параметра " + "server1c.lockTime");
		Ожидаем.Что(МенеджерПараметров.Параметр("server1c.lockTime")).Существует();
		
		МассивОбслуживаемыхБаз = МассивОбслуживаемыхБаз();
		Для Каждого База Из МассивОбслуживаемыхБаз Цикл
			Лог.Отладка("Тестирование параметра server1c.bases." + База);
			Ожидаем.Что(МенеджерПараметров.Параметр("server1c.bases." + База)).Существует();
			Лог.Отладка("Тестирование параметра server1c.bases." + База + ".user");
			Ожидаем.Что(МенеджерПараметров.Параметр("server1c.bases." + База + ".user")).Существует();
			Лог.Отладка("Тестирование параметра server1c.bases." + База + ".password");
			Ожидаем.Что(МенеджерПараметров.Параметр("server1c.bases." + База + ".password")).Существует();
			Лог.Отладка("Тестирование параметра server1c.bases." + База + ".allowKey");
			Ожидаем.Что(МенеджерПараметров.Параметр("server1c.bases." + База + ".allowKey")).Существует();
		КонецЦикла;
		
		Для Каждого База Из МассивОбслуживаемыхБаз Цикл
			НастройкиХранилищ = МенеджерПараметров.Параметр("server1c.bases." + База + ".1cstorages", Неопределено);
			Если ТипЗнч(НастройкиХранилищ) <> Неопределено Тогда
				МассивХранилищ = Новый Массив;
				Для Каждого Хранилище Из МенеджерПараметров.Параметр("server1c.bases." + База + ".1cstorages") Цикл
					МассивХранилищ.Добавить(Хранилище.Ключ);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;

		// проверки подключений
		РезультатПодключения = ПолучениеСоединенийСКластером();
		Если РезультатПодключения.Результат Тогда
			ТекущийКластер = РезультатПодключения.Кластер;
		Иначе
			ЗавершитьРаботуСкрипта(1);
		КонецЕсли;

		Лог.Информация("Тестирование подключения подключений к хранилищам");
		Если МассивХранилищ.Количество() > 0 Тогда
			Для Каждого База Из МассивОбслуживаемыхБаз Цикл
				Лог.Информация("Тестирование хранилищ базы " + База);
				Конфигуратор = ПараметрыКонфигуратора(База);
				Для Каждого Хранилище Из МассивХранилищ Цикл
					ПользовательХранилища = МенеджерПараметров.Параметр("server1c.bases." + База + ".1cstorages." + Хранилище + ".user");
					ПарольХранилища = МенеджерПараметров.Параметр("server1c.bases." + База + ".1cstorages." + Хранилище + ".pass");
					ПутьКХранилищу = МенеджерПараметров.Параметр("server1c.bases." + База + ".1cstorages." + Хранилище + ".path");
					ИмяХранилища = МенеджерПараметров.Параметр("server1c.bases." + База + ".1cstorages." + Хранилище + ".name");
					ЭтоРасширение = ЭтоХранилищеРасширения(База, Хранилище, ИмяХранилища);
					Конфигуратор.УстановитьМеткуДляВерсииВХранилище(ПутьКХранилищу,
						ПользовательХранилища,
						ПарольХранилища,
						"0",
						"",
						0,
						?(ЭтоРасширение.Результат, ЭтоРасширение.ИмяРасширения, ""));
					Лог.Информация(Конфигуратор.ВыводКоманды());
					Лог.Информация("Тестирование подключения хранилища " + Хранилище + " к базе " + База + " завершено");
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
		Лог.Информация("Тестирование параметров завершено");
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
		Лог.Ошибка("Тестирование не пройдено: (" + ОписаниеОшибки + ")");
	КонецПопытки;

КонецФункции

#КонецОбласти



#Область СлужебныеПроцедурыИФункции

Функция ЭтоХранилищеРасширения(База, Хранилище, ИмяХранилища)
	
	СохраненноеИмяХранилища = МенеджерПараметров.Параметр("server1c.bases." + База + ".1cstorages." + Хранилище + ".name");
	Возврат Новый Структура("Результат,ИмяРасширения", "Main" <> СохраненноеИмяХранилища, СохраненноеИмяХранилища);
	
КонецФункции

Функция МассивОбслуживаемыхБаз()
	Результат = Новый Массив;
	Для Каждого База Из МенеджерПараметров.Параметр("server1c.bases") Цикл
		Результат.Добавить(База.Ключ);
	КонецЦикла;
	Возврат Результат;
КонецФункции

Функция СтрокаСоединенияКонфигуратора(База, Конфигуратор)
	
	ПараметрыСтрокиСоединения = Конфигуратор.ПараметрыСтрокиСоединения();
	ПараметрыСтрокиСоединения.Сервер = МенеджерПараметров.Параметр("server1c.server");
	ПараметрыСтрокиСоединения.Порт = МенеджерПараметров.Параметр("server1c.port");
	ПараметрыСтрокиСоединения.ИмяБазы = База;
	Возврат ПараметрыСтрокиСоединения;
	
КонецФункции

Функция ПараметрыСоединенияСИнформационнойБазой(ИмяТекущейБазы,ТекущийКластер)

	ИнформационнаяБаза = ТекущийКластер.ИнформационныеБазы().Получить(ИмяТекущейБазы);
	АдминистраторПользовательИБ = МенеджерПараметров.Параметр("server1c.bases." + ИмяТекущейБазы + ".user");
	ПарольАдминистратораИБ = МенеджерПараметров.Параметр("server1c.bases." + ИмяТекущейБазы + ".password");
	ИнформационнаяБаза.УстановитьАдминистратора(АдминистраторПользовательИБ, ПарольАдминистратораИБ);
	Возврат ИнформационнаяБаза;
	
КонецФункции

Функция ПараметрыКонфигуратора(База)
	
	Конфигуратор = Новый УправлениеКонфигуратором();
	Конфигуратор.ИспользоватьВерсиюПлатформы(МенеджерПараметров.Параметр("server1c.version"));
	ПараметрыСтрокиСоединения = СтрокаСоединенияКонфигуратора(База, Конфигуратор);
	Конфигуратор.УстановитьКонтекст(ПараметрыСтрокиСоединения,
		МенеджерПараметров.Параметр("server1c.bases." + База + ".user"),
		МенеджерПараметров.Параметр("server1c.bases." + База + ".password"));
	
	КлючРазрешенияЗапуска = МенеджерПараметров.Параметр("server1c.bases." + База + ".allowKey");
	Конфигуратор.УстановитьКлючРазрешенияЗапуска(КлючРазрешенияЗапуска);
	
	ФайлПлатформы = ОбъединитьПути(ТекущийКаталог(), "tt.txt");
	Конфигуратор.УстановитьИмяФайлаСообщенийПлатформы(ФайлПлатформы);
	Возврат Конфигуратор;
	
КонецФункции

// Запущен
//		Проверяет запущен ли процесс сервера RAS
// Возвращаемое значение:
//   Булево - Истина - если процесс запущен, Ложь - в остальных случаях
//
Функция ЗапущенRAS() Экспорт
	
	Если ПроцессRAS = Неопределено Тогда
		Возврат Ложь;
	Иначе
		Возврат НЕ ПроцессRAS.Завершен;
	КонецЕсли;
	
КонецФункции

// Остановить
// 	Останавливает запущенный сервер RAS.
//
Процедура ОстановитьRAS() Экспорт
	
	Если НЕ ЗапущенRAS() Тогда
		Возврат;
	КонецЕсли;
	
	ПроцессRAS.Завершить();
	
	Лог.Отладка("Процесс %1 ras завершен.", ПроцессRAS.Идентификатор);
	
	ПроцессRAS = Неопределено;
	
КонецПроцедуры

Функция ЗапуститьRAS()
	
	Если ЗапущенRAS() Тогда
		Возврат ПроцессRAS;
	КонецЕсли;
	
	ПутьК1С = МенеджерПараметров.Параметр("serverRAS.pathToBin1c");
	RAS = ПутьК1С + "\" + МенеджерПараметров.Параметр("server1c.version") + "\bin\ras.exe";
	Порт = "--port=" + МенеджерПараметров.Параметр("serverRAS.mportRAS")
		+ " " + МенеджерПараметров.Параметр("server1c.server")
		+ ":" + МенеджерПараметров.Параметр("server1c.portCluster1C");
	
	ПараметрыКоманды = Новый Массив;
	ПараметрыКоманды.Добавить(ОбернутьВКавычки(RAS));
	ПараметрыКоманды.Добавить("cluster");
	ПараметрыКоманды.Добавить(Порт);
	КоманднаяСтрока = СтрСоединить(ПараметрыКоманды, " ");
	
	
	ТекущийКаталог = ТекущийКаталог();
	ПеренаправлятьПотокВывода = Истина;
	ПеренаправлятьПотокВвода = Ложь;
	КодировкаПотоков = КодировкаТекста.Системная;
	
	Лог.Отладка("Строка запуска RAS: " + КоманднаяСтрока);
	
	ПроцессRAS = СоздатьПроцесс(КоманднаяСтрока, ТекущийКаталог,
			ПеренаправлятьПотокВывода, ПеренаправлятьПотокВвода, КодировкаПотоков);
	
	ПроцессRAS.Запустить();
	
	ИмяСервераRAS = "localhost:" + МенеджерПараметров.Параметр("serverRAS.mportRAS");
	
	Возврат ПроцессRAS;
	
КонецФункции

Функция НуженЗапускRAS()
	
	Возврат НЕ МенеджерПараметров.Параметр("serverRAS.useDemonRAS");
	
КонецФункции

Функция ОбернутьВКавычки(Строка)
	Возврат СтрШаблон("""%1""", Строка);
КонецФункции

Функция Форматировать(Знач Уровень, Знач Сообщение) Экспорт
	
	Возврат СтрШаблон("%1: %2 - %3", ТекущаяДата(), УровниЛога.НаименованиеУровня(Уровень), Сообщение);
	
КонецФункции

Функция ЗавершитьРаботуСкрипта(Код)
	
	Конфигуратор = NULL;
	ОстановитьRAS();
	ФайлПлатформы = ОбъединитьПути(ТекущийКаталог(), "tt.txt");
	Если ФС.ФайлСуществует(ФайлПлатформы) Тогда
		ФС.УдалитьФайлы(ФайлПлатформы);
	КонецЕсли;
	Лог.Закрыть();
	ВыполнитьСборкуМусора();
	ЗавершитьРаботу(Код);
	
КонецФункции

Процедура ОбработкаКомандыСкрипта(КомандаСкрипта)
	
	Если КомандаСкрипта = "testparam"
		ИЛИ КомандаСкрипта = "debug" Тогда
		Лог.УстановитьУровень(УровниЛога.Отладка);
		ТестированиеПараметров();
	ИначеЕсли КомандаСкрипта = "lock" Тогда
		БлокировкаНаБазы(МассивОбслуживаемыхБаз, Истина);
	ИначеЕсли КомандаСкрипта = "unlock" Тогда
		БлокировкаНаБазы(МассивОбслуживаемыхБаз, Ложь);
	ИначеЕсли КомандаСкрипта = "restart" Тогда
		ПерезапускСервера();
	ИначеЕсли КомандаСкрипта = "updateFromStorage" Тогда
		УдалениеСеансовСКонфигуратором(МассивОбслуживаемыхБаз);
		Для Каждого База Из МассивОбслуживаемыхБаз Цикл
			ОбновлениеКонфигурацииИзХранилища(База);
		КонецЦикла;
	ИначеЕсли КомандаСкрипта = "updateDB" Тогда
		УдалениеВсехСеансов(МассивОбслуживаемыхБаз);
		Для Каждого База Из МассивОбслуживаемыхБаз Цикл
			ОбновлениеКонфигурации(База);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти





КомандаСкрипта = ИнициализацияОкружения();
МассивКоммандПакетногоРежима = СтрРазделить(МенеджерПараметров.Параметр("batchМode"), ",");

Если КомандаСкрипта = "batch" Тогда
	Для Индекс = 0 По МассивКоммандПакетногоРежима.Количество() - 1 Цикл
		КомандаИзПакета = МассивКоммандПакетногоРежима.Получить(Индекс);
		ОбработкаКомандыСкрипта(КомандаИзПакета);
	КонецЦикла;
ИначеЕсли КомандаСкрипта = "test" Тогда
Иначе
	ОбработкаКомандыСкрипта(КомандаСкрипта);
КонецЕсли;

ЗавершитьРаботуСкрипта(0);
